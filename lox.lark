?start: program
program: declaration*
declaration: var_decl | statement
var_decl: "var" IDENTIFIER ( "=" expression )? ";"
statement: expr_stmt | print_stmt | block | if_stmt
block: "{" declaration* "}"
expr_stmt: expression ";"
print_stmt: "print" expression ";"
if_stmt: "if" "(" expression ")" statement ("else" statement)?
expression: assignment
assignment: IDENTIFIER "=" assignment | equality
equality:  comparsion ( ( "!=" | "==" ) comparsion )*
comparsion: term ( ( ">" | ">=" | "<" | "<=" ) term )*
term: factor ( ( "-" | "+") factor )*
factor:  unary (( "/" | "*" ) unary )*
unary: ( "!" | "-" ) unary
     | primary
primary: NUMBER | STRING | BOOLEAN | NULL | "(" expression ")" | IDENTIFIER


STRING: /"[^"]*"/
NUMBER: /\d+/
IDENTIFIER: /[a-zA-Z_][a-zA-Z_0-9]*/
BOOLEAN: "true" | "false"
NULL: "nil"

%import common.WS
%ignore WS
